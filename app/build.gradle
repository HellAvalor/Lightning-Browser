apply plugin: 'com.android.application'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionName "4.4.1"
        vectorDrawables.useSupportLibrary = true
    }

    sourceSets {
        lightningPlus.setRoot('src/LightningPlus')
        lightningLite.setRoot('src/LightningLite')
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles 'proguard-project.txt'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles 'proguard-project.txt'
        }
    }
    flavorDimensions "default"
    productFlavors {
        lightningPlus {
            buildConfigField "boolean", "FULL_VERSION", "true"
            applicationId "acr.browser.lightning"
            versionCode 90
        }

        lightningLite {
            buildConfigField "boolean", "FULL_VERSION", "false"
            applicationId "acr.browser.barebones"
            versionCode 92
        }
    }

    lintOptions {
        abortOnError true
    }

    packagingOptions {
        exclude '.readme'
    }
}

dexcount {
    includeClasses = false
    includeFieldCount = false
  //  printAsTree = false
    orderByMethodCount = true
    verbose = false
}

dependencies {

    // support libraries
    implementation 'com.android.support:palette-v7:28.+'
    implementation 'com.android.support:appcompat-v7:28.+'
    implementation 'com.android.support:design:28.+'
    implementation 'com.android.support:recyclerview-v7:28.+'
    implementation 'com.android.support:support-v4:28.+'

    // html parsing for reading mode
    implementation 'org.jsoup:jsoup:1.9.2'

    // event bus
    implementation 'com.squareup:otto:1.3.8'

    // dependency injection
    implementation 'com.google.dagger:dagger:2.0.2'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.0.2'
    compileOnly 'javax.annotation:jsr250-api:1.0'

    // view binding
    implementation 'com.jakewharton:butterknife:7.0.1'
    annotationProcessor 'com.jakewharton:butterknife:7.0.1'

    // permissions
    implementation 'com.anthonycr.grant:permissions:1.1.2'

    // proxy support
    implementation 'net.i2p.android:client:0.8'

    // Use the following code to update the libnetcipher submodule
    // git submodule foreach git reset --hard
    // git submodule update --remote
    implementation project(':libnetcipher')

    implementation project(':bonsai')

    implementation project(':animated-progress-bar')

    // memory leak analysis
    debugApi 'com.squareup.leakcanary:leakcanary-android:1.4'
    releaseApi 'com.squareup.leakcanary:leakcanary-android-no-op:1.4'
}